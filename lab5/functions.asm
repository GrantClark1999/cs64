# xSpim Memory Demo Program

#  Data Area
.data

space:
    .asciiz " "

newline:
    .asciiz "\n"

dispArray:
    .asciiz "\nCurrent Array:\n"

convention:
    .asciiz "\nConvention Check\n"

myArray:
	.word 0 33 123 -66 332 -1 -223 453 9 45 -78 -14  

#Text Area (i.e. instructions)
.text

main:
    ori     $v0, $0, 4          
    la      $a0, dispArray 
    syscall

    ori     $s1, $0, 12
    la      $s0, myArray

    add     $a1, $0, $s1
    add     $a0, $0, $s0
 
    jal     DispArray

    ori     $s2, $0, 0
    ori     $s3, $0, 0
    ori     $s4, $0, 0
    ori     $s5, $0, 0
    ori     $s6, $0, 0
    ori     $s7, $0, 0
    
    add     $a1, $0, $s1
    add     $a0, $0, $s0

    jal     PrintReverse

    add     $s1, $s1, $s2
    add     $s1, $s1, $s3
    add     $s1, $s1, $s4
    add     $s1, $s1, $s5
    add     $s1, $s1, $s6
    add     $s1, $s1, $s7

    add     $a1, $0, $s1
    add     $a0, $0, $s0
    jal     DispArray

    j       Exit

DispArray:
    addi    $t0, $0, 0 
    add     $t1, $0, $a0

dispLoop:
    beq     $t0, $a1, dispend
    sll     $t2, $t0, 2
    add     $t3, $t1, $t2
    lw      $t4, 0($t3)

    ori     $v0, $0, 1
    add     $a0, $0, $t4
    syscall

    ori     $v0, $0, 4
    la      $a0, space
    syscall

    addi    $t0, $t0, 1
    j       dispLoop    

dispend:
    ori     $v0, $0, 4
    la      $a0, newline
    syscall
    jr      $ra 

ConventionCheck:
    addi    $t0, $0, -1
    addi    $t1, $0, -1
    addi    $t2, $0, -1
    addi    $t3, $0, -1
    addi    $t4, $0, -1
    addi    $t5, $0, -1
    addi    $t6, $0, -1
    addi    $t7, $0, -1
    ori     $v0, $0, 4
    la      $a0, convention
    syscall
    addi $v0, $zero, -1
    addi $v1, $zero, -1
    addi $a0, $zero, -1
    addi $a1, $zero, -1
    addi $a2, $zero, -1
    addi $a3, $zero, -1
    addi $k0, $zero, -1
    addi $k1, $zero, -1
    jr      $ra
    
Exit:
    ori     $v0, $0, 10
    syscall

# COPYFROMHERE - DO NOT REMOVE THIS LINE

PrintReverse:
    addi $sp, $sp, -4
    sw $ra, 0($sp)

    addi $a1, $a1, -1       # make $a1 be the index = size - 1
    sll $a1, $a1, 2         # mutliply index by 4

    jal PrintLoop

    lw $ra, 0($sp)          # restore return address
    addi $sp, $sp, 4

    jr $ra

PrintLoop:
    addi $sp, $sp, -12
    sw $ra, 8($sp)          # store return address to caller onto stack
    sw $s0, 4($sp)
    sw $s1, 0($sp)

    move $s0, $a0
    move $s1, $a1

    add $t0, $s0, $s1
    lw $a0, 0($t0)          # $a0 points to the last non-printed element of the array

    li $v0, 1               # print last non-printed element of the array
    syscall

    jal ConventionCheck

    addi $s1, $s1, -4       # decrement $s1 by 4
    blt $s1, $zero, Return  # if $s1 is less than 0, return from function

    move $a0, $s0
    move $a1, $s1
    jal PrintLoop           # else, print loop again, with new args

Return:
    lw $ra, 8($sp)          # restore stack
    lw $s0, 4($sp)
    lw $s1, 0($sp)          
    addi $sp, $sp, 12

    jr $ra                  # return to caller